plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.121'

}

group = project.mod_group_id
version = project.mod_version

base {
    archivesName = project.mod_id
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven {
        url = "https://maven.blamejared.com"
        content { includeGroup "mezz.jei" }
    }

    // EMI (обычно releases, snapshots можно оставить вторым)
    maven {
        url = "https://maven.firstdark.dev/releases"
        content { includeGroup "dev.emi" }
    }
    maven {
        url = "https://maven.firstdark.dev/snapshots"
        content { includeGroup "dev.emi" }
    }

    // Modrinth (Sodium/Iris у тебя именно отсюда)
    maven {
        url = "https://api.modrinth.com/maven"
        content { includeGroup "maven.modrinth" }
    }

    maven { url = "https://maven.firstdark.dev/snapshots" }

    maven { url = "https://www.cursemaven.com" }
    maven { url = "https://maven.terraformersmc.com/releases" }
    maven { url = "https://maven.shedaniel.me/" } // Cloth/REI и т.п.
    maven { url = "https://jitpack.io" }
    maven { url = "https://maven.neoforged.net/releases" }
}


dependencies {

    implementation "javazoom:jlayer:1.0.1"
    jarJar(implementation("javazoom:jlayer:1.0.1")) {
        version {
            // This mod is developed and tested with 1.0.1.
            strictly "[1.0.1,1.0.2)"
            prefer "1.0.1"
        }
    }

    implementation 'com.mysql:mysql-connector-j:8.0.33'
    jarJar(implementation(group:'com.mysql', name:'mysql-connector-j')) {
        version {
            strictly "[8.0.33,)"
            prefer '8.0.33'
        }
    }

    // Connection pool for MySQL logging
    implementation('com.zaxxer:HikariCP:5.1.0') {
        // Avoid bundling SLF4J API if the runtime already provides it
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    jarJar(implementation(group: 'com.zaxxer', name: 'HikariCP', version: '5.1.0')) {
        version {
            strictly "[5.1.0,)"
            prefer '5.1.0'
        }
    }
    // LDLib2 (bundle via Jar-in-Jar)
    def ldlib2Dep = "com.lowdragmc.ldlib2:ldlib2-neoforge-1.21.1:2.0.1:all"
    implementation(ldlib2Dep)
    jarJar(implementation(ldlib2Dep)) {
        version {
            strictly "[2.0.1,)"
            prefer "2.0.1"
        }
    }

    implementation("org.appliedenergistics.yoga:yoga:1.0.0")
    jarJar("org.appliedenergistics.yoga:yoga:1.0.0")


}



neoForge {
    version = project.neo_version

    runs {
        client {
            client()
        }
        server {
            server()
        }
        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').absolutePath, '--existing', file('src/main/resources/').absolutePath
        }
    }

    mods {
        "${project.mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs = ['src/main/resources', 'src/generated/resources']
        }
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(ProcessResources).configureEach {
    // Expand properties in neoforge.mods.toml
    def props = [
            mod_id: project.mod_id,
            mod_name: project.mod_name,
            mod_version: project.mod_version,
            mod_authors: project.mod_authors,
            mod_description: project.mod_description,
            minecraft_version: project.minecraft_version,
            neo_version: project.neo_version
    ]

    inputs.properties props

    filesMatching('META-INF/neoforge.mods.toml') {
        expand props
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 21
}
